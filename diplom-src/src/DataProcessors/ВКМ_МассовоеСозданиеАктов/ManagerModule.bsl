#Область ПрограммныйИнтерфейс

Функция СоздатьДокументыРеализации(НачалоПериода, КонецПериода) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокДокументов = Новый ТаблицаЗначений;
	СписокДокументов.Колонки.Добавить("Договоры");
	СписокДокументов.Колонки.Добавить("Реализации");
	
	ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание;
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_СоздатьДокументыРеализации();
		
	Запрос.УстановитьПараметр("Абонент", ВидДоговора);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ДокРеализации) Тогда
			Продолжить;
		Иначе
			
			НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(НовыйДок, Выборка);
			НовыйДок.Заполнить(Выборка.ДокОбслуживаниеКлиентов);
			НовыйДок.Дата = ТекущаяДата();
			НовыйДок.Основание = Выборка.ДокОбслуживаниеКлиентов;
			
			НовыйДок.Записать(РежимЗаписиДокумента.Проведение);
			
			Строка = СписокДокументов.Добавить();
			Строка.Договоры = Выборка.Договор;
			Строка.Реализации = НовыйДок.Ссылка;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат СписокДокументов;
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

 Функция ТекстЗапроса_СоздатьДокументыРеализации()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК ДокРеализации,
	               |	ВКМ_ОбслуживаниеКлиентов.Ссылка КАК ДокОбслуживаниеКлиентов,
	               |	ВКМ_ОбслуживаниеКлиентов.Клиент КАК Контрагент,
	               |	ВКМ_ОбслуживаниеКлиентов.Специалист КАК Ответственный,
	               |	ВКМ_ОбслуживаниеКлиентов.Договор.Организация КАК Организация,
	               |	ВКМ_ОбслуживаниеКлиентов.Договор КАК Договор
	               |ИЗ
	               |	Документ.ВКМ_ОбслуживаниеКлиентов КАК ВКМ_ОбслуживаниеКлиентов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО ВКМ_ОбслуживаниеКлиентов.Ссылка = РеализацияТоваровУслуг.Основание
	               |ГДЕ
	               |	ВКМ_ОбслуживаниеКлиентов.Дата >= &НачалоПериода
	               |	И ВКМ_ОбслуживаниеКлиентов.Дата <= &КонецПериода
	               |	И ВКМ_ОбслуживаниеКлиентов.Договор.ВидДоговора = &Абонент";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти