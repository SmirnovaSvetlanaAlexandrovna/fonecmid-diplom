#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения();
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижения()

	СформироватьДвиженияОсновныеНачисленияИУдержания();
	
	СформироватьДвиженияВзаиморасчетыПоСотрудникам();
	
	РасчитатьОтпуск();
		
КонецПроцедуры

Процедура СформироватьДвиженияОсновныеНачисленияИУдержания()

	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Оклад = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад;
	Отпуск = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск;
	ПроцентОтРабот = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПроцентОтРабот;
	БазовыйПериодНачало = ДобавитьМесяц(ТекущаяДата(), -12);
	БазовыйПериодКонец = ТекущаяДата();
	
	ДатаНачала = НачалоМесяца(ТекущаяДата());
	ДатаОкончания = КонецМесяца(ТекущаяДата()); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_СформироватьДвиженияОсновныеНачисленияИУдержания();
	
	Граница = Новый Граница(ТекущаяДата(), ВидГраницы.Включая);
	
	Запрос.УстановитьПараметр("Период", Граница);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ТипЗнч(Выборка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда
			
			Если Выборка.ВидРасчета = Оклад Тогда
			
				Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
				Движение.ПериодРегистрации = Дата;
				Движение.Регистратор = Ссылка;
				Движение.ПериодДействияНачало = ДатаНачала;
				Движение.ПериодДействияКонец = ДатаОкончания;
				Движение.Сторно = Ложь;
				Движение.ВидРасчета = Оклад;
				Движение.Сотрудник = Выборка.Сотрудник;
				Движение.Результат = ПрименитьНДФЛ(Выборка.Оклад);
				
			ИначеЕсли Выборка.ВидРасчета = Отпуск Тогда
				
				Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
				Движение.ПериодРегистрации = Дата;
				Движение.Регистратор = Ссылка;
				Движение.ПериодДействияНачало = Выборка.ОтпускДатаС;
				Движение.ПериодДействияКонец = Выборка.ОтпускДатаПо;
				Движение.БазовыйПериодНачало = БазовыйПериодНачало;
				Движение.БазовыйПериодКонец = БазовыйПериодКонец;
				Движение.Сторно = Ложь;
				Движение.ВидРасчета = Отпуск;
				Движение.Сотрудник = Выборка.Сотрудник;
				Движение.Результат = 0;
				
				
				Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
				Движение = Движения.ВКМ_ОтпускаСотрудников.Добавить();
				Движение.Период = Дата;
				Движение.Сотрудник = Выборка.Сотрудник;
				Движение.КоличествоДнейОтпускаФакт = (Выборка.ОтпускДатаПо - Выборка.ОтпускДатаС) / 86400;
				
			ИначеЕсли Выборка.ВидРасчета = ПроцентОтРабот Тогда
				
				Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
				Движение.ПериодРегистрации = Дата;
				Движение.Регистратор = Ссылка;
				Движение.ПериодДействияНачало = ДатаНачала;
				Движение.ПериодДействияКонец = ДатаОкончания;
				Движение.Сторно = Ложь;
				Движение.ВидРасчета = ПроцентОтРабот;
				Движение.Сотрудник = Выборка.Сотрудник;
				Движение.Результат = ПрименитьНДФЛ(Выборка.СуммаКОплате);
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Выборка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_Удержания") Тогда
			
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.Регистратор = Ссылка;
			Движение.Сторно = Ложь;
			Движение.ВидРасчета = Выборка.ВидРасчета;
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.Результат = Выборка.Размер;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();
	Движения.ВКМ_ОтпускаСотрудников.Записать();
	
КонецПроцедуры

Процедура РасчитатьОтпуск()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отпуск = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск;
	КоличествоРабочихДней = ПолучитьКоличествоРабочихДней();
	
	Если КоличествоРабочихДней = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Данные по отпускам не могут быть расчитаны, заполните График работы!");
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_РасчитатьОтпуск();
		
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Отпуск", Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
			
		КоличествоДнейОтпуска = Выборка.КоличествоДнейОтпуска;
		
		СуммаЗа12Месяцев = Выборка.СуммаЗа12Месяцев;
		
		Результат = (СуммаЗа12Месяцев/КоличествоРабочихДней)*КоличествоДнейОтпуска;
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		Движение.Результат = ПрименитьНДФЛ(Результат);
		
	КонецЦикла;
		
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура СформироватьДвиженияВзаиморасчетыПоСотрудникам()

	УстановитьПривилегированныйРежим(Истина);
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_СформироватьДвиженияВзаиморасчетыПоСотрудникам();
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Если ЗначениеЗаполнено(Выборка.ОсновноеНачисление) И ЗначениеЗаполнено(Выборка.Удержание) Тогда
			Сумма = Выборка.ОсновноеНачисление - Выборка.Удержание;
		ИначеЕсли ЗначениеЗаполнено(Выборка.ОсновноеНачисление) И НЕ ЗначениеЗаполнено(Выборка.Удержание) Тогда
			Сумма = Выборка.ОсновноеНачисление;
		ИначеЕсли НЕ ЗначениеЗаполнено(Выборка.ОсновноеНачисление) И ЗначениеЗаполнено(Выборка.Удержание) Тогда
			Сумма = - Выборка.Удержание;
		КонецЕсли;
		
		Если Сумма >= 0 Тогда
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Иначе
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Сумма = - Сумма; 
		КонецЕсли;
		
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник; 
		Движение.Сумма = Сумма;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьКоличествоРабочихДней()
	
	УстановитьПривилегированныйРежим(Истина);

	НачалоПериода = ДобавитьМесяц(ТекущаяДата(), -12);
	ОкончаниеПериода = ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ВКМ_ГрафикиРаботы.Значение) КАК Количество
		|ИЗ
		|	РегистрСведений.ВКМ_ГрафикиРаботы КАК ВКМ_ГрафикиРаботы
		|ГДЕ
		|	ВКМ_ГрафикиРаботы.Дата >= &НачалоПериода
		|	И ВКМ_ГрафикиРаботы.Дата <= &ОкончаниеПериода
		|	И ВКМ_ГрафикиРаботы.Значение <> 0";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Количество;	
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПрименитьНДФЛ(Сумма)

	Вычет = Сумма * 13 / 100;
	Результат = Сумма - Вычет;
	Возврат Результат;
	
КонецФункции


#Область ТекстыЗапросов

Функция ТекстЗапроса_РасчитатьОтпуск()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	               |	РАЗНОСТЬДАТ(ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало, ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец, ДЕНЬ) КАК КоличествоДнейОтпуска,
	               |	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДополнительныеНачисления.РезультатБаза, 0) КАК СуммаЗа12Месяцев,
	               |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	               |			Регистратор = &Ссылка
	               |				И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияДанныеГрафика
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	               |				&Измерения,
	               |				&Измерения,
	               |				,
	               |				Регистратор = &Ссылка
	               |					И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияБазаОсновныеНачисления
	               |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(
	               |				&Измерения,
	               |				&Измерения,
	               |				,
	               |				Регистратор = &Ссылка
	               |					И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияБазаДополнительныеНачисления
	               |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДополнительныеНачисления.НомерСтроки";
	
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапроса_СформироватьДвиженияОсновныеНачисленияИУдержания()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВКМ_ВыполненныеСотрудникомРаботы.Сотрудник КАК Сотрудник,
	               |	СУММА(ВКМ_ВыполненныеСотрудникомРаботы.СуммаКОплате) КАК СуммаКОплате
	               |ПОМЕСТИТЬ вт_ВыполненныеСотрудникомРаботыЗаПериод
	               |ИЗ
	               |	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы КАК ВКМ_ВыполненныеСотрудникомРаботы
	               |ГДЕ
	               |	ВКМ_ВыполненныеСотрудникомРаботы.Период >= &НачалоПериода
	               |	И ВКМ_ВыполненныеСотрудникомРаботы.Период <= &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВКМ_ВыполненныеСотрудникомРаботы.Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВКМ_НачислениеЗаработнойПлатыНачисления.Сотрудник КАК Сотрудник,
	               |	ВКМ_НачислениеЗаработнойПлатыНачисления.ВидРасчета КАК ВидРасчета,
	               |	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад,
	               |	ВКМ_НачислениеЗаработнойПлатыНачисления.Размер КАК Размер,
	               |	вт_ВыполненныеСотрудникомРаботыЗаПериод.СуммаКОплате КАК СуммаКОплате,
	               |	ВКМ_НачислениеЗаработнойПлатыНачисления.ОтпускДатаС КАК ОтпускДатаС,
	               |	ВКМ_НачислениеЗаработнойПлатыНачисления.ОтпускДатаПо КАК ОтпускДатаПо
	               |ИЗ
	               |	Документ.ВКМ_НачислениеЗаработнойПлаты.Начисления КАК ВКМ_НачислениеЗаработнойПлатыНачисления
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, ) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	               |		ПО ВКМ_НачислениеЗаработнойПлатыНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
	               |		ЛЕВОЕ СОЕДИНЕНИЕ вт_ВыполненныеСотрудникомРаботыЗаПериод КАК вт_ВыполненныеСотрудникомРаботыЗаПериод
	               |		ПО ВКМ_НачислениеЗаработнойПлатыНачисления.Сотрудник = вт_ВыполненныеСотрудникомРаботыЗаПериод.Сотрудник
	               |ГДЕ
	               |	ВКМ_НачислениеЗаработнойПлатыНачисления.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапроса_СформироватьДвиженияВзаиморасчетыПоСотрудникам()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	               |	СУММА(ЕСТЬNULL(ВКМ_ОсновныеНачисления.Результат, 0)) КАК Результат
	               |ПОМЕСТИТЬ вт_ОсновныеНачисления
	               |ИЗ
	               |	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	               |ГДЕ
	               |	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВКМ_ОсновныеНачисления.Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	               |	СУММА(ЕСТЬNULL(ВКМ_Удержания.Результат, 0)) КАК Результат
	               |ПОМЕСТИТЬ вт_Удержания
	               |ИЗ
	               |	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	               |ГДЕ
	               |	ВКМ_Удержания.Регистратор = &Регистратор
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВКМ_Удержания.Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(вт_ОсновныеНачисления.Сотрудник, вт_Удержания.Сотрудник) КАК Сотрудник,
	               |	ЕСТЬNULL(вт_ОсновныеНачисления.Результат, 0) КАК ОсновноеНачисление,
	               |	ЕСТЬNULL(вт_Удержания.Результат, 0) КАК Удержание
	               |ИЗ
	               |	вт_ОсновныеНачисления КАК вт_ОсновныеНачисления
	               |		ПОЛНОЕ СОЕДИНЕНИЕ вт_Удержания КАК вт_Удержания
	               |		ПО вт_ОсновныеНачисления.Сотрудник = вт_Удержания.Сотрудник";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
